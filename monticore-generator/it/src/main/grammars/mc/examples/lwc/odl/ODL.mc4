/*
 * ******************************************************************************
 * MontiCore Language Workbench, www.monticore.de
 * Copyright (c) 2017, MontiCore, All rights reserved.
 *
 * This project is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3.0 of the License, or (at your option) any later version.
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this project. If not, see <http://www.gnu.org/licenses/>.
 * ******************************************************************************
 */

package mc.examples.lwc.odl;

/**
* Object Definition Language (ODL) for Language Workbench Comparison (LWC).
* This language defines instances of the EDL entities, 
* including assignment of values to the properties.
*/
grammar ODL extends mc.grammar.lexicals.Lexicals {
     
  token INT  = ('0'..'9')+;  
  
  ////////// PRODUCTIONS //////////
  
  ODLCompilationUnit =
    ("package" package:(Name& || ".")+ ";")? 
    (MCImportStatement)*
    Instances;
    
  MCImportStatement = "import" importList:(Name& || ".")* ("." star:["*"])? ";" ;
    
  Instances = "instances" Name "{" (Object)* "}";

  Object = type:QualifiedName Name "=" "{" (Assignment)* "}";
	
  Assignment = Name "=" Value;

	
  interface Value;
		
  StringValue implements Value = STRING;
  
  IntValue implements Value = INT;
  
  DateValue implements Value = Date; 

  ReferenceValue implements Value = Name;
  
  
  Date = day:INT "." month:INT "." year:INT;
  
  QualifiedName = (Name || ".")*;
  
  fragment token ESC =
    '\\' ('n' |
    'r' |
    't' |
    '"' |
    '\\' );

  token STRING =
    '"' ( ESC |
      ~('"' |
      '\\' |
      '\n' |
      '\r' )
    )*
	'"' : {setText(getText().substring(1, getText().length() - 1));};

    
}
