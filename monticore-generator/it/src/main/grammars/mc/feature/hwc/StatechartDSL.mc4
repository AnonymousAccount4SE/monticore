/*
 * ******************************************************************************
 * MontiCore Language Workbench, www.monticore.de
 * Copyright (c) 2017, MontiCore, All rights reserved.
 *
 * This project is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3.0 of the License, or (at your option) any later version.
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this project. If not, see <http://www.gnu.org/licenses/>.
 * ******************************************************************************
 */

package mc.feature.hwc; 

component grammar StatechartDSL extends mc.common.Basics {      

  Statechart implements SCStructure= "statechart" Name "{" (states:State | transitions:Transition | userCode:Code)* "}" EOF;
  
  EntryAction= "entry" ":" block:BlockStatement;
  
  ExitAction= "exit" ":" block:BlockStatement;  
    
   
  State implements SCStructure = "state" Name ("<<" (["initial"] | ["final"])* ">>")?
    ( ("{ options{}" (EntryAction)? (ExitAction)? (states:State | transitions:Transition)* "}") | ";") ;
 
  Transition = from:Name "->" to:Name 
    				(":" (event:Name ( "(" (arguments:Argument ( "," arguments:Argument)*) ")" )? )?
  					 ("[" guard: Expression "]")?
  					 ("/" action: BlockStatement)? ";" 
  				| ";");  
    
  Argument= paramType:Name paramName:Name;  
   
  Code= "code" body: Classbody ;
    
  interface SCStructure; 
  
  astrule SCStructure =
  	Name
  	states:State*
  	transitions:Transition*
  	method public String toString() {{return name;}} 
  ;
  
  astrule State = 
  	method public String toString() {
  		return name; 
  	} ;
  
  external BlockStatement;
  
  external Expression;
  
  external Classbody;
  
 }
