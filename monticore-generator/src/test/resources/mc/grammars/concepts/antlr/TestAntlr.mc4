/*
 * ******************************************************************************
 * MontiCore Language Workbench, www.monticore.de
 * Copyright (c) 2017, MontiCore, All rights reserved.
 *
 * This project is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3.0 of the License, or (at your option) any later version.
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this project. If not, see <http://www.gnu.org/licenses/>.
 * ******************************************************************************
 */

package mc.grammars.concepts.antlr;

component grammar TestAntlr extends mc.grammars.lexicals.TestLexicals {

    /** Used for embedding java declarations */
	external AntlrCode;
	
    /** Used for embedding ANTLR statements */
	external ActionAntlr;

	/** An AntlrConcept allows to specify additional antlr rules and Java code for Parser and lexer
	@attribute AntlrParserCode Additional Antlr rules for the parser
	@attribute AntlrParserAction Additional Java code for the parser
	@attribute AntlrLexerCode Additional Antlr rules for the lexer
	@attribute AntlrLexerAction Additional Java code for the lexer
	*/	
	ConceptAntlr = 
		"{"
		   (AntlrParserCode:AntlrParserCode | AntlrParserAction:AntlrParserAction |
		    AntlrLexerCode:AntlrLexerCode | AntlrLexerAction:AntlrLexerAction)*
		"}";
		
	astrule ConceptAntlr =
		method public String toString() {
			return "";
		};
	
	
	/** Additional Antlr rules for the parser
	@attribute Text	Antlr Parser rules
	*/
	AntlrParserCode = 
		"parser" "antlr" Name? "{" Text:ActionAntlr "}";

	/** Additional Java code for the parser
	@attribute Text	Java Code
	*/		
	AntlrParserAction= 
		"parser" "java" "{" Text:AntlrCode "}"; // MemberDeclarations
		
	/** AntlrLexerCode Additional Antlr rules for the lexer
	@attribute Text	Antlr lexer rules
	*/
	AntlrLexerCode= 
		"lexer" "antlr" Name? "{" Text:ActionAntlr "}";
	
	/** Additional Additional Java code for the lexer
	@attribute Text	Java Code
	*/
	AntlrLexerAction= 
		"lexer" "java" "{" Text:AntlrCode "}";	// MemberDeclarations
}


	
