package de.monticore.grammar;

mlc Grammar {

  //export the grammar
  export "$projectDir/src/main/grammars" {
    include "de/monticore/grammar/Grammar.mc4";
  }

  //export handwritten code
  export "$projectDir/src/main/java" {
    include "de/monticore/grammar/grammar/**.java";

    include "de/monticore/grammar/prettyprint/GrammarPrettyPrinter.java";

    include "de/monticore/grammar/DirectLeftRecursionDetector.java";
    include "de/monticore/grammar/LexNamer.java";
    include "de/monticore/grammar/MultiplicityVisitor.java";
    include "de/monticore/grammar/PredicatePair.java";
    include "de/monticore/grammar/RegExpBuilder.java";

    include "de/monticore/grammar/cocos/**.java";
  }

  // export all Java files generated from the grammar
  export "$projectDir/target/generated-sources/monticore/sourcecode" {
    include "de/monticore/grammar/grammar/**.java";
  }

  promote {
    mlc "de.monticore.literals.MCCommonLiterals";
    mlc "de.monticore.types.MCSimpleGenericTypes";
  }

  uses {
    include "$mp/org/apache/commons/lang3/StringUtils.class";
  }
}
