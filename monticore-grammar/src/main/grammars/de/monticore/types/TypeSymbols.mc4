/* (c) https://github.com/MontiCore/monticore */
package de.monticore.types;

/* Beta-version: This is intended to become a MontiCore stable grammar. */

/**
 * This grammar is used to generate an abstract Syntax only.
 * These AST-classes are then used to store typing information
 * in the SymbolTable.
 *
 * The grammar introduces the symbols for
 * Types, Methods, Fields and TypeVariables
 *
 * These symbols can be used elsewhere, when actual implementations
 * of these symbols become available.
 *
*/

component grammar TypeSymbols extends de.monticore.MCBasics {

  /*=================================================================*/

  /**
   * A type (in an OO sense) has a name,
   * methods, fields, supertypes.
   * All of them are represented as Symbols.
   * TODO BR: !  What to do with: typeParameters (then it should not be a type, but a typeconstructor)
   * TODO: BR das wuerde ich noch gerne verstehen: Wo sind die TypParameter?
   */
  // TODO BR -> MB (6.6.) : Type einbauen (als Oberklasse von OOType)
  interface scope symbol Type = Name;
  symbolrule Type =
    superTypes: de.monticore.types.check.SymTypeExpression*
    isClass: boolean
    isInterface: boolean
    isEnum: boolean
    isAbstract: boolean
    isPrivate: boolean
    isProtected: boolean
    isPublic: boolean
    isStatic: boolean
  ;

  /*=================================================================*/

  /**
   * A typeVar may be bounded (see Java)
   */
  interface symbol TypeVar extends Type = Name;

  /*=================================================================*/

  /**
   * A Field (Java Attribute) has a type
   */
  // TODO BR -> MB (6.6.) : Variable einbauen (als Oberklasse von Field)
  interface symbol Field = Name ;
  symbolrule Field =
    type: de.monticore.types.check.SymTypeExpression
    isParameter: boolean
    isVariable: boolean
    isPrivate: boolean
    isProtected: boolean
    isPublic: boolean
    isStatic:boolean
  ;

  /*=================================================================*/

  /**
   * A Method has a return type, and a list of parameters, which
   * are stored as field symbols.
   */
  // TODO BR -> MB (6.6.) : Function einbauen (als Oberklasse von Method)
  interface scope symbol Method = Name;
  symbolrule Method =
    returnType: de.monticore.types.check.SymTypeExpression
    isConstructor: boolean
    isMethod: boolean
    isPrivate: boolean
    isProtected: boolean
    isPublic: boolean
    isStatic:boolean
  ;

}
