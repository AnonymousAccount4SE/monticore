/* (c) https://github.com/MontiCore/monticore */
package de.monticore.types;

/**
 * This grammar introduces basic generics List<>, Map<,>, Set<> and Optional<>
*/
component grammar MCBasicGenericsTypes extends de.monticore.types.MCBasicTypes {

  start Type;

  /**
  *  The interface ASTBasicGenerics can be used to restrict to BasicGenerics even if a extended Grammar
  * (MCGenericTypes.mc4) is included.
  */
  interface GenericReferenceType extends ReferenceType;

  astrule GenericReferenceType =
    // names:List<String>
    // arguments:List<de.monticore.types.mcbasicgenericstypes._ast.ASTGenericArgument>
  ;

  // use LookAhead to avoid "List" etc. as keywords

  ListType implements GenericReferenceType <200> = { _input.LT(1).getText().equals("List") }? Name "<" TypeArgument ">";

  OptionalType implements GenericReferenceType <200> = { _input.LT(1).getText().equals("Optional") }? Name "<" TypeArgument ">";

  MapType implements GenericReferenceType  <200> = { _input.LT(1).getText().equals("Map") }? Name "<" key:TypeArgument "," value:TypeArgument ">";

  SetType implements GenericReferenceType <200> = { _input.LT(1).getText().equals("Set") }? Name "<" TypeArgument ">";

  /**
  *  ASTTypeArgument interface introduced here as QualifiedReferenceType, more implementations to be added in subgrammars
  */
  interface TypeArgument ;

  BasicTypeArgument implements TypeArgument = QualifiedReferenceType;


}